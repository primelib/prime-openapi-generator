// Apply the gradle plugins
plugins {
    id 'java'
    id 'org.hidetake.swagger.generator' version '2.18.2'
}

// Dependencies
dependencies {
    // CodeGenerator
    swaggerCodegen group: 'org.openapitools', name: 'openapi-generator-cli', version: '5.2.1'
    swaggerCodegen project(':' + rootProject.name + '-java-feign')
    swaggerCodegen project(':' + rootProject.name + '-template-engine')

    // Annotations
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.1.10'

    // Jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.5'

    // Feign
    implementation group: 'io.github.openfeign', name: 'feign-core', version: '11.6'
}

// OpenAPI Sources
swaggerSources {
    helix {
        inputFile = file('src/main/resources/openapi.yaml')
        code {
            language = 'custom-java-feign'
            configuration = configurations.swaggerCodegen
            configFile = file("src/main/resources/openapi.json")
            outputDir = file("$rootDir/usage-example")
            rawOptions = Collections.singletonList("--engine=auto")
            components = [models: true, modelTests: true, apis: true, apiTests: true]
            wipeOutputDir = false
        }
    }
}

// depend on jar task of the generators
swaggerSources*.code*.dependsOn ':' + rootProject.name + '-java-feign:clean'
swaggerSources*.code*.dependsOn ':' + rootProject.name + '-java-feign:jar'
